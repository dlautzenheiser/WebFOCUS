-* File: IBFS:/localhost/EDA/EDASERVE/APPPATH/bcbsm/Procedure1.fex Created by WebFOCUS AppStudio
-SET &ECHO = 'OFF';

-DEFAULT &GROUP_TYPE = '';
-DEFAULT &GRP_LIST = '';
-SET &GROUP = '&' || 'GROUP';
-SET &COUNT = '&' || 'COUNT';
-SET &GRP_LIST_WITH_TICKS = '';


-* strip out blanks (should already be done by web page but just in case)
-SET &INLENGTH = &GRP_LIST.LENGTH;
-SET &GRP_LIST = STRIP(&INLENGTH.EVAL, '&GRP_LIST.EVAL', ' ', 'A&INLENGTH.EVAL');

-* loop through the groups separated by commas
-SET &COMMASTRING = &GRP_LIST;
-SET &NBRCOMMAS = 0;
-SET &ONGRPNBR = 1;
-CNTCOMMAS
-SET &POSCOMMAS = POSIT('&COMMASTRING.EVAL', &COMMASTRING.LENGTH, ',', 1, 'I3');
-SET &NBRCOMMAS = IF &POSCOMMAS NE 0 THEN &NBRCOMMAS + 1 ELSE &NBRCOMMAS;
-SET &NEWSTART = &POSCOMMAS + 1;
-SET &NEWLENGTH = &COMMASTRING.LENGTH - &POSCOMMAS;
-SET &NEWSTRING = SUBSTR(&COMMASTRING.LENGTH, '&COMMASTRING.EVAL', &NEWSTART.EVAL, &COMMASTRING.LENGTH, &NEWLENGTH, 'A&NEWLENGTH.EVAL');
-SET &STRIPLN = IF &POSCOMMAS IS 0 THEN &COMMASTRING.LENGTH ELSE &POSCOMMAS-1;

-SET &STRIPPED = SUBSTR(&COMMASTRING.LENGTH,'&COMMASTRING.EVAL',1,&STRIPLN,&STRIPLN,'A&STRIPLN.EVAL');
-SET &GROUP&ONGRPNBR.EVAL = '''' || &STRIPPED || '''';
-SET &SEPARATOR = IF &ONGRPNBR IS 1 THEN '' ELSE ',';
-SET &GRP_LIST_WITH_TICKS = &GRP_LIST_WITH_TICKS || &SEPARATOR || &GROUP&ONGRPNBR.EVAL ;
-SET &ONGRPNBR = &ONGRPNBR + 1;

-SET &COMMASTRING = &NEWSTRING;
-IF &POSCOMMAS NE 0 THEN GOTO CNTCOMMAS;
-SET &NBRGROUPS = &NBRCOMMAS + 1;


-TYPE NBRGROUPS=&NBRGROUPS
-TYPE GROUP_TYPE=&GROUP_TYPE
-TYPE GRP_LIST=&GRP_LIST
-TYPE GRP_LIST_WITH_TICKS=&GRP_LIST_WITH_TICKS
-* testing: showing all the individual groups
-SET &COUNT = 1;
-LOOPSPLIT
-TYPE GROUP&COUNT.EVAL=&GROUP&COUNT.EVAL
-SET &COUNT = &COUNT + 1;
-IF &COUNT LE &NBRGROUPS THEN GOTO LOOPSPLIT;


