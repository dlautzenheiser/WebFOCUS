-SET &ECHO = 'OFF';

-DEFAULT &SearchString = 'ALL';
-DEFAULT &LastRunStatus = '_FOC_NULL';

-DEFAULT &Active = 'Y';
-DEFAULT &IntervalType = '_FOC_NULL';
-DEFAULT &Details = 'N';
-DEFAULT &WFFMT = 'HTML';

-PROMPT &Active.(<Active,Y>,<Inactive,N>,<All,_FOC_NULL>).Active?.;
-PROMPT &LastRunStatus.(<All,_FOC_NULL>,<Failed,FAILURE>,<Warning,WARNING>,<No Errors,NOERROR>).Last Run Status.; 
-PROMPT &IntervalType.(<All,_FOC_NULL>,<On-Demand,O>,<Custom,C>,<Daily,D>,<Weekly,W>,<Monthly,M>,<Yearly,Y>).Run Interval.; 
-PROMPT &Details.(<No,N>,<Yes,Y>).Show Packet and Task Names?.; 
-PROMPT &WFFMT.(<Online,HTML>,<Spreadsheet,XLSX>).Output Format.;

-SET &SearchString = IF '&SearchString.EVAL' IS '' OR 'ALL' OR 'all' OR 'All' THEN '_FOC_NULL' ELSE '&SearchString.EVAL'; 
-SET &SearchStringDesc = IF '&SearchString.EVAL' IS '_FOC_NULL' THEN '' ELSE 'with names or descriptions containing ' | ('''' || &SearchString.EVAL || '''') || ' (not case sensitive)';

-SET &UPSEARCH = UPCASE(&SearchString.LENGTH, '&SearchString.EVAL', 'A&SearchString.LENGTH'); 
-SET &UPSEARCH2 = STRREP(&UPSEARCH.LENGTH, '&UPSEARCH.EVAL', 1, ' ', 1, '_', &UPSEARCH.LENGTH, 'A&UPSEARCH.LENGTH'); 
-TYPE UPSEARCH="&UPSEARCH.EVAL"

-SET &IntervalTypeDesc = IF '&IntervalType.EVAL' IS 'O' THEN 'On-Demand' ELSE IF '&IntervalType.EVAL' IS 'W' THEN 'Weekly' ELSE
- IF '&IntervalType.EVAL' IS 'D' THEN 'Daily' ELSE
- IF '&IntervalType.EVAL' IS 'M' THEN 'Monthly' ELSE 
- IF '&IntervalType.EVAL' IS 'C' THEN 'Custom' ELSE 
- IF '&IntervalType.EVAL' IS 'Y' THEN 'Yearly' ELSE 
- IF '&IntervalType.EVAL' IS '_FOC_NULL' THEN 'All' ELSE 'Unknown'; 

-SET &ActiveDesc = IF '&Active.EVAL' IS '_FOC_NULL' THEN 'All' ELSE IF '&Active.EVAL' IS 'Y' THEN 'Active' ELSE IF '&Active.EVAL' IS 'N' THEN 'Inactive' ELSE 'Unknown';
-SET &LastRunStatusDesc = IF '&LastRunStatus.EVAL' IS '_FOC_NULL' THEN 'All' ELSE IF '&LastRunStatus.EVAL' IS 'NOERROR' THEN 'No Errors' ELSE
- IF '&LastRunStatus.EVAL' IS 'FAILURE' THEN 'Failed' ELSE IF '&LastRunStatus.EVAL' IS 'WARNING' THEN 'Warning' ELSE 'Unknown';

JOIN SCHEDULEID IN BOTSCHED TO SCHEDULEID IN BOTSIT AS J1
JOIN PACKETID IN BOTSCHED TO PACKETID IN BOTTASK AS J3
JOIN LEFT_OUTER PACKETID IN BOTSCHED TO PACKETID IN BOTPACK AS J4

JOIN LEFT_OUTER CUSTOMID IN BOTSCHED TO CUSTOMID IN BOTSCIT AS J2 
JOIN LEFT_OUTER CUSTOMID IN BOTSCHED TO CUSTOMID IN BOTCDATE AS J5
DEFINE FILE BOTSCHED
ACTIVEDESC/A10 = IF ACTIVE IS 'Y' THEN 'Active' ELSE IF ACTIVE IS 'N' THEN 'Inactive' ELSE 'Unknown';
ENDDATE_YYMD/A8YYMD = ENDDATE; 
STARTDATE_YYMD/A8YYMD = STARTDATE;
ENDTIME_FMTD/A5 = EDIT(ENDTIME,'99:99');
STARTTIME_FMTD/A5 = EDIT(STARTTIME,'99:99');
INTERVALDESC/A10 = DECODE INTERVALTYPE('O' 'On-Demand' 'W' 'Weekly' 'D' 'Daily' 'M' 'Monthly' 'C' 'Custom' 'Y' 'Yearly' ELSE 'Unknown');
MON/A1 = EDIT(WEEKDAYS,'9$$$$$$');
TUE/A1 = EDIT(WEEKDAYS,'$9$$$$$');
WED/A1 = EDIT(WEEKDAYS,'$$9$$$$');
THU/A1 = EDIT(WEEKDAYS,'$$$9$$$');
FRI/A1 = EDIT(WEEKDAYS,'$$$$9$$');
SAT/A1 = EDIT(WEEKDAYS,'$$$$$9$');
SUN/A1 = EDIT(WEEKDAYS,'$$$$$$9');
FULLDESC/A20 = IF INTERVALTYPE IS 'O' THEN 'Run by User' ELSE IF INTERVALTYPE IS 'C' THEN 'Custom Interval' ELSE IF MON IS '1' THEN 'Mo' ELSE ' ';
FULLDESC/A20 = IF TUE IS '1' AND FULLDESC IS ' ' THEN 'Tu' ELSE IF TUE IS '1' THEN FULLDESC || ',Tu' ELSE FULLDESC;
FULLDESC/A20 = IF WED IS '1' AND FULLDESC IS ' ' THEN 'We' ELSE IF WED IS '1' THEN FULLDESC || ',We' ELSE FULLDESC;
FULLDESC/A20 = IF THU IS '1' AND FULLDESC IS ' ' THEN 'Th' ELSE IF THU IS '1' THEN FULLDESC || ',Th' ELSE FULLDESC;
FULLDESC/A20 = IF FRI IS '1' AND FULLDESC IS ' ' THEN 'Fr' ELSE IF FRI IS '1' THEN FULLDESC || ',Fr' ELSE FULLDESC;
FULLDESC/A20 = IF SAT IS '1' AND FULLDESC IS ' ' THEN 'Sa' ELSE IF SAT IS '1' THEN FULLDESC || ',Sa' ELSE FULLDESC;
FULLDESC/A20 = IF SUN IS '1' AND FULLDESC IS ' ' THEN 'Su' ELSE IF SUN IS '1' THEN FULLDESC || ',Su' ELSE FULLDESC;

JOBDESC_A/A64 = JOBDESC; 
PACKETNAME_A/A64 = DESC1; 
TASKNAME_A/A64 = TASKNAME;
UPJOBDESC/A64 = UPCASE(64, JOBDESC_A, UPJOBDESC); 
UPPACKETNAME/A64 = UPCASE(64, PACKETNAME_A, UPPACKETNAME); 
UPTASKNAME/A64 = UPCASE(64, TASKNAME_A, UPTASKNAME);

NUM_NEXTRUNTIME/D14 WITH NEXTRUNTIME = EDIT(NEXTRUNTIME) ;
START_DATE/I8YYMD WITH NEXTRUNTIME = 19700101;
START_YYMD/YYMD=START_DATE;
SCHEDULE_START/HYYMDIA = HDTTM(START_YYMD, 8, 'HYYMDIA');
NEXT_RUNTIME/HYYMDIA MISSING ON = IF ACTIVE IS 'N' THEN MISSING ELSE IF NUM_NEXTRUNTIME IS 9223372036854775807 THEN MISSING ELSE HADD(SCHEDULE_START,'MILLISECONDS', NUM_NEXTRUNTIME, 8, 'HYYMDIA');
NEXT_RUNTIME/HYYMDIA MISSING ON = HADD(NEXT_RUNTIME, 'HOURS', -5, 8, 'HYYMDIA'); 
LAST_RUNTIME/YYMD MISSING ON = LASTEXTIME; 
REALLYACTIVE/A1 = IF (ACTIVE IS 'Y' AND LASTEXSTATUS IS 'FAILURE') THEN 'N' ELSE 
 IF (INTERVALTYPE IS 'O' AND ACTIVE IS 'Y' AND LAST_RUNTIME IS MISSING) THEN 'N' ELSE  
 IF (INTERVALTYPE IS 'O' AND ACTIVE IS 'Y' AND LAST_RUNTIME LT '&DATEYY.EVAL') THEN 'N' ELSE 
 IF (INTERVALTYPE NE 'O' AND ACTIVE IS 'Y' AND NEXT_RUNTIME IS MISSING) THEN 'N' ELSE 'Y'; 

END
TABLE FILE BOTSCHED 
WHERE ACTIVE IS '&Active.EVAL';
WHERE INTERVALTYPE IS '&IntervalType.EVAL'; 
WHERE LASTEXSTATUS IS '&LastRunStatus.EVAL'; 
WHERE UPJOBDESC CONTAINS '&UPSEARCH.EVAL' OR UPPACKETNAME CONTAINS '&UPSEARCH.EVAL' OR UPTASKNAME CONTAINS '&UPSEARCH.EVAL'; 

WRITE REALLYACTIVE  NOPRINT 

BY ACTIVEDESC AS 'Status' UNDER-LINE 
-* BY JOBDESC AS 'Job Description'
-* BY CASTER_USER AS 'RC UserID'
-* BY NOTIFY_SUBJECT AS 'Notify Subject'
-* BY FULLDISTLIST AS 'Distribution List'

BY INTERVALDESC AS 'Interval'
BY JOBDESC AS 'Job Description'
BY FULLDESC AS 'Days Run'


-* BY STARTTIMEZONE AS 'Start Timezone'
BY STARTDATE_YYMD AS 'Started,Running'
-* BY STARTTIME_FMTD AS 'Started,Time'
BY ENDDATE_YYMD AS 'Stops,Running'
-* BY ENDTIME_FMTD AS 'Stops,Time'

BY LAST_RUNTIME AS 'Last Run'
BY LASTEXSTATUS AS 'Last Status'
BY NEXT_RUNTIME AS 'Next Run' 

-IF &Details IS 'N' THEN GOTO SKIP_DETAIL; 
BY DESC1 AS 'Packet Name'
BY TASKNAME AS 'Task Name'
BY TASKOBJPATH AS 'Task Object Path'
-SKIP_DETAIL 


HEADING 
"Search Scheduled Jobs &SearchStringDesc.EVAL "
"Status: &ActiveDesc.EVAL / Last Run Status: &LastRunStatusDesc / Interval Type: &IntervalTypeDesc.EVAL " 
" "

ON TABLE SET DROPBLNKLINE ALL
ON TABLE SET EMPTYREPORT ON 
ON TABLE SET BYDISPLAY ON 
ON TABLE PCHOLD FORMAT &WFFMT AS 'Search Results for Scheduled Jobs'   
ON TABLE SET HTMLCSS ON
ON TABLE SET LINES 999999
ON TABLE SET STYLE *
INCLUDE=IBFS:/FILE/IBI_HTML_DIR/ibi_themes/Warm.sty,$
TYPE=HEADING,
    JUSTIFY=CENTER,$
TYPE=REPORT,
     TITLETEXT='Content Repository Search Results',
     SQUEEZE=ON, ORIENTATION=LANDSCAPE,$
     
TYPE=DATA,
     COLUMN=N1, COLOR='RED', STYLE=ITALIC,
     WHEN=REALLYACTIVE EQ 'N', $   
TYPE=DATA,
     COLUMN=NEXT_RUNTIME, COLOR='RED', STYLE=ITALIC,
     WHEN=LASTEXSTATUS EQ 'FAILURE', $  
TYPE=DATA,
     COLUMN=LASTEXSTATUS, COLOR='RED', STYLE=ITALIC,
     WHEN=LASTEXSTATUS EQ 'FAILURE', $   
TYPE=DATA,
     COLUMN=LASTEXSTATUS, COLOR='BROWN', STYLE=ITALIC,
     WHEN=LASTEXSTATUS EQ 'WARNING', $
TYPE=DATA,
     COLUMN=NEXT_RUNTIME, COLOR='BROWN', STYLE=ITALIC,
     WHEN=LASTEXSTATUS EQ 'WARNING', $  
ENDSTYLE
END
-RUN 