-SET &ECHO = 'OFF';

-DEFAULT &SearchString = 'EnterSearchString';
-DEFAULT &WFFMT = 'HTML';

-PROMPT &SearchString.Enter search string (not case sensitive).; 
-PROMPT &WFFMT.(<Online,HTML>,<Spreadsheet,XLSX>).View.;

-SET &SearchStringDesc = 'with code containing ' | ('''' || '&SearchString.EVAL' || '''') || ' (not case sensitive)';

-SET &UPSEARCH = UPCASE(&SearchString.LENGTH, '&SearchString.EVAL', 'A&SearchString.LENGTH'); 
-SET &UPSEARCH2 = STRREP(&UPSEARCH.LENGTH, '&UPSEARCH.EVAL', 1, ' ', 1, '_', &UPSEARCH.LENGTH, 'A&UPSEARCH.LENGTH'); 
-TYPE UPSEARCH="&UPSEARCH.EVAL"

ENGINE SQLJDBC SET DEFAULT_CONNECTION WFRepository
SQL SQLJDBC SELECT
T2.OBJNAME,T2.PRT_PATH,T1.OBJ_HANDLE,
CAST(CAST(BCONTENT AS VARBINARY(MAX)) AS VARCHAR(MAX)) AS CODE
FROM dbo.WF_CONTENT_REVS T1
    INNER JOIN dbo.WF_REPOSOBJ T2 ON T1.OBJ_HANDLE = T2.HANDLE
WHERE UPPER(CAST(CAST(BCONTENT AS VARBINARY(MAX)) AS VARCHAR(MAX))) LIKE '%&UPSEARCH.EVAL%'
; 
TABLE FILE SQLOUT 
BY OBJ_HANDLE/A1000 AS 'OBJHANDLE'
ON TABLE SET ASNAMES ON 
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS OBJECTLIST FORMAT ALPHA 
END 
-RUN 
-IF &FOCERRNUM NE 0 THEN GOTO ERROR; 


JOIN LEFT_OUTER HANDLE IN WF_REPOSOBJ TAG HOST TO UNIQUE OBJ_HANDLE IN WF_NLSOBJ TAG PROC AS J1 
END
-* JOIN LEFT_OUTER TOPFOLDER WITH PRT_PATH IN WF_REPOSOBJ TAG HOST TO UNIQUE OBJ_HANDLE IN WF_NLSOBJ TAG FLDR AS J2 
-* END 
DEFINE FILE WF_REPOSOBJ
OBJHANDLE/A1000 = HANDLE; 
PRTPATH/A1000 = PRT_PATH; 
STRIPPATH1/A1000 = SUBSTR(1000,PRTPATH,17,1000,1000-17,STRIPPATH1); 
IPOS1/I3 = POSIT(STRIPPATH1, 1000, '/', 1, IPOS1);
TOPFOLDER/A100 = IF IPOS1 IS 0 THEN STRIPPATH1 ELSE SUBSTR(1000,STRIPPATH1,1,IPOS1-1,IPOS1,TOPFOLDER); 

TYPEDESC/A30=DECODE OBJTYPE( 102 'Rptg Obj' 103 'Stylesheet' 106 'Help HTM' 107 'CSS' 109 'Procedure' 110 'HTML' 
                             113 'Schedule' 117 'URL' 4 'Other' 0 'Static Web Component' 301 'User Entry' 115 'Distrib List' 118 'RptLib Content' 
                             128 'Page Strings' 131 'Page Grids' 6001 'Page Resource' 4 'JavaScript' ELSE 'Unknown');
IGNORE/I1 = IF OBJTYPE IS 4 OR 118 THEN 1 ELSE 0; 
OBJNAME_A/A64 = OBJNAME; 
OBJDESC_A/A64 = PROC.OBJDESC;
UPOBJNAME/A64 = UPCASE(64, OBJNAME_A, UPOBJNAME); 
UPOBJDESC/A64 = UPCASE(64, OBJDESC_A, UPOBJDESC);
END 
TABLE FILE WF_REPOSOBJ
WHERE CLASSNAME IS 'WfItem';
WHERE OBJTYPE IS 109 ;
PRINT 
 CREATEDBY/A20 AS 'Created By'
 CREATEDON/YYMD AS 'Created On'
 LASTMODON/YYMD AS 'Last Modified On'
 LASTACCESSON/YYMD AS 'Last Accessed On'
BY PRT_PATH/A100 AS 'Path'  
-* BY STRIPPATH1 
-* BY TOPFOLDER 
-* BY FLDR.OBJDESC AS 'Top Folder Title'
BY TYPEDESC AS 'Object Type'
BY OBJTYPE AS 'Obj Type,Code'
BY PROC.OBJDESC AS 'Title' 
BY OBJNAME AS 'Name'
WHERE WF_LNG IS 'en_US'; 

WHERE OBJHANDLE IN FILE OBJECTLIST; 

ON PRT_PATH UNDER-LINE SUBHEAD
"Path: <PRT_PATH Top Folder: <TOPFOLDER " 
HEADING
"WebFOCUS Procedures &SearchStringDesc.EVAL "
" " 

ON TABLE SET DROPBLNKLINE ALL
ON TABLE SET EMPTYREPORT ON 
ON TABLE SET BYDISPLAY ON 
ON TABLE PCHOLD AS 'WebFOCUS_Code_Search_Results' FORMAT &WFFMT  
ON TABLE SET HTMLCSS ON
ON TABLE SET LINES 999999
ON TABLE SET STYLE *
INCLUDE=IBFS:/FILE/IBI_HTML_DIR/ibi_themes/Warm.sty,$
TYPE=REPORT,
     TITLETEXT='Code Search Results',
     SQUEEZE=ON, ORIENTATION=LANDSCAPE,$
ENDSTYLE
END
-RUN
-IF &FOCERRNUM NE 0 THEN GOTO ERROR; 
-GOTO END_PROC 



-ERROR
-TYPE *ERROR* An error has occurred.  
-GOTO END_PROC 


-END_PROC 
-TYPE *DONE* The process has finished.
