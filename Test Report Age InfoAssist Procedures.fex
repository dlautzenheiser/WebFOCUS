-SET &ECHO = 'ALL';
-* -DEFAULT &SearchString = 'string';
-* -PROMPT &SearchString.Enter search string (not case sensitive).; 

-* -SET &UPSEARCH = UPCASE(&SearchString.LENGTH, '&SearchString.EVAL', 'A&SearchString.LENGTH'); 
-* -TYPE UPSEARCH="&UPSEARCH.EVAL"

-DEFAULT &SORTFIELD = 'FOLDERNAME';
-DEFAULT &WFFMT = 'HTML';

-PROMPT &SORTFIELD.(<Folder Name,FOLDERNAME>,<CreatedBy UserID,CREATEDBY>).Summarize by.;
-PROMPT &WFFMT.(<Online,HTML>,<Spreadsheet,XLSX BYTOC 1>).Output format.;

-SET &SORTFIELD_DESC = IF &SORTFIELD IS 'FOLDERNAME' THEN 'Folder Name' ELSE IF &SORTFIELD IS 'CREATEDBY' THEN 'CreatedBy UserID' ELSE 'BADSORTFIELD';


-*  FILENAME=  WF_REPOSOBJ
-*  HANDLE        handle        A255V
-*  CLASSNAME     classname     A20V
-*  APPNAME       appname       A256V
-*  CREATEDBY     createdby     A128V
-*  CREATEDON     createdon     HYYMDm
-*  DEF_LNG       def_lng       A6V
-*  EXP_DATE      exp_date      HYYMDm
-*  EXT_ID        ext_id        A90V
-*  LASTACCESSBY  lastaccessby  A128V
-*  LASTACCESSON  lastaccesson  HYYMDm
-*  LASTMODBY     lastmodby     A128V
-*  LASTMODON     lastmodon     HYYMDm
-*  OBJNAME       objname       A64V
-*  OBJTYPE       objtype       I11
-*  PRT_PATH      prt_path      A256V
-*  RSNAME        rsname        A256V
-*  SRT_ORDER     srt_order     I11


-* FILENAME=  UOA_USERS
-* ID            ID            I11
-* CONCURENT     CONCURENT     I11
-* DESCRIPTION   DESCRIPTION   A255V
-* EMAIL         EMAIL         A255V
-* LASTLOGIN     LASTLOGIN     HYYMDs
-* NAME          NAME          A255V
-* NAMESPACE     NAMESPACE     A255V
-* NUMBADTRIES   NUMBADTRIES   I11
-* PASSWORD      PASSWORD      A255V
-* PASSWORDSETON PASSWORDSETON HYYMDs
-* PRIMGROUPID   PRIMGROUPID   I11
-* PROPERTIES    PROPERTIES    A4000V
-* STATUS        STATUS        A255V


DEFINE FILE UOA_USERS
USERID/A100 = NAME; 
IPOS1/I3 = POSIT(USERID, 100, '@', 1, IPOS1); 
ILENGTH/I3 = ARGLEN(100, USERID, ILENGTH); 
DOMAIN/A100 = SUBSTR(100, USERID, IPOS1+1, ILENGTH, ILENGTH-IPOS1, DOMAIN); 
APROPS/A4000 = PROPERTIES; 
IPOS2/I3 = POSIT(APROPS, 4000, 'SEATDATE=', 1, IPOS2); 
SEATDATE_YYYYMMDD/A8 = SUBSTR(4000, APROPS, IPOS2+9, IPOS2+17, 8, SEATDATE_YYYYMMDD); 
NEWSTATUS/A20 = IF STATUS IS 'INACTIVE' THEN 'INACTIVE' ELSE IF LASTLOGIN IS MISSING THEN 'NOLOGIN' ELSE IF STATUS IS 'ACTIVE' THEN 'ACTIVE' ELSE IF STATUS IS 'MUSTCHANGE' THEN 'NOLOGIN' ELSE 'UNKNOWN'; 
USERTYPE/A20 = IF USERID CONTAINS '@' THEN 'External' ELSE 'Internal'; 
COUNTER/I5C = 1; 
END
TABLE FILE UOA_USERS
BY USERID 
BY NEWSTATUS AS 'USERSTATUS'

ON TABLE SET ASNAMES ON 
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS XREF_SECURITYCENTER_USERS_USERID FORMAT FOCUS INDEX USERID 
END
-RUN 
-IF &FOCERRNUM NE 0 THEN GOTO ERROR;
-IF &LINES IS 0 THEN GOTO ERROR; 

-* TABLE FILE XREF_SECURITYCENTER_USERS_USERID
-* PRINT * 
-* END 
-* -RUN 
-* -EXIT 



JOIN LEFT_OUTER CREATEDBY IN WF_REPOSOBJ TO USERID IN XREF_SECURITYCENTER_USERS_USERID AS J1
DEFINE FILE WF_REPOSOBJ
NEWSTATUS/A20 = IF USERSTATUS IS '' THEN 'NOTUSER' ELSE USERSTATUS; 
TYPEDESC/A30=DECODE OBJTYPE( 102 'Reporting Object' 103 'Stylesheet' 106 'Help HTM' 107 'CSS' 109 'WF Procedure' 110 'HTML' 
                             113 'Schedule' 117 'URL' 4 'Other' 0 'Static Web Component' 301 'User Entry' 115 'Distribution List' 118 'Report Library Content' ELSE 'Unknown');
IGNORE/I1 = IF OBJTYPE IS 4 OR 118 THEN 1 ELSE 0; 
OBJNAME_A/A64 = OBJNAME; 
UPOBJNAME/A64 = UPCASE(64, OBJNAME_A, OBJNAME); 
LASTACCESSON_YEAR/YY = LASTACCESSON; 
YEARSTATUS/A20 = IF LASTACCESSON_YEAR LE 2021 THEN 'Inactive' ELSE 'Active'; 
FOCEXEC/A256 = PRT_PATH;
STRIPPATH/A256 = SUBSTR(256,FOCEXEC,17,256,256-17,STRIPPATH); 
IPOS/I3 = POSIT(STRIPPATH, 256, '/', 1, IPOS); 
FOLDERNAME/A256 = IF IPOS IS 0 THEN STRIPPATH ELSE SUBSTR(256, STRIPPATH, 1, IPOS-1, IPOS, FOLDERNAME); 

END
TABLE FILE WF_REPOSOBJ
-* WHERE PRT_PATH LIKE '/WFC/Repository/IA_%';
WHERE CLASSNAME IS 'WfItem';
WHERE OBJTYPE IS 109;

COUNT OBJNAME/I5C AS 'Count'
ACROSS LASTACCESSON_YEAR AS 'Year Last Accessed' ROW-TOTAL AS 'Total'
BY YEARSTATUS AS 'Status Based on 2022' SUB-TOTAL AS 'Total'
BY &SORTFIELD AS '&SORTFIELD_DESC.EVAL' 
ON TABLE COLUMN-TOTAL AS 'Grand Total' 

HEADING
"Age of WebFOCUS Procedures in Content Repository Sorted by &SORTFIELD_DESC.EVAL"
"Considering those Accessed in 2022 as Active"

ON TABLE SET BYDISPLAY ON 
ON TABLE PCHOLD AS 'Age of WebFOCUS Procedures' FORMAT &WFFMT  
ON TABLE SET HTMLCSS ON
ON TABLE SET LINES 999999
ON TABLE SET STYLE *
TYPE=REPORT,
     TITLETEXT='Age of WebFOCUS Procedures',
     SQUEEZE=ON, ORIENTATION=LANDSCAPE,$
     INCLUDE=IBFS:/FILE/IBI_HTML_DIR/javaassist/intl/EN/ENIADefault_combine.sty,$

ENDSTYLE
END
-RUN 
-IF &FOCERRNUM NE 0 THEN GOTO ERROR;
-IF &LINES IS 0 THEN GOTO ERROR; 


-GOTO END_PROC 


-ERROR
-TYPE *ERROR* An error has occurred.  
-GOTO END_PROC 


-END_PROC 
-TYPE *DONE* The process has finished.
