-SET &ECHO = 'ALL';
 
-PROMPT &BODYPARM.Enter Bodytypes with separating commas and in upper case (for example: SEDAN, COUPE, ROADSTER):.
 
-SET &SEARCHSTRING = &BODYPARM;
 
-* strip out blanks
-SET &INLENGTH = &SEARCHSTRING.LENGTH;
-SET &SEARCHSTRING = STRIP(&INLENGTH.EVAL, '&SEARCHSTRING.EVAL', ' ', 'A&INLENGTH.EVAL');
 
-* count the number of commas (to calculate STRREP result length)
-SET &COMMASTRING = &SEARCHSTRING;
-SET &NBRCOMMAS = 0;
-CNTCOMMAS
-SET &POSCOMMAS = POSIT('&COMMASTRING.EVAL', &COMMASTRING.LENGTH, ',', 1, 'I3');
-SET &NBRCOMMAS = IF &POSCOMMAS NE 0 THEN &NBRCOMMAS + 1 ELSE &NBRCOMMAS;
-SET &NEWSTART = &POSCOMMAS + 1;
-SET &NEWLENGTH = &COMMASTRING.LENGTH - &POSCOMMAS;
-SET &NEWSTRING = SUBSTR(&COMMASTRING.LENGTH, '&COMMASTRING.EVAL', &NEWSTART.EVAL, &COMMASTRING.LENGTH, &NEWLENGTH, 'A&NEWLENGTH.EVAL');
-SET &COMMASTRING = &NEWSTRING;
-IF &POSCOMMAS NE 0 THEN GOTO CNTCOMMAS;
 
-* two-step process to get ticks around string values
-SET &NEWLENGTH = &SEARCHSTRING.LENGTH + (&NBRCOMMAS * 4);
-SET &NEWSTRING = STRREP(&SEARCHSTRING.LENGTH, '&SEARCHSTRING.EVAL', 1, ',', 6, '# OR #', &NEWLENGTH.EVAL, 'A&NEWLENGTH.EVAL');
-SET &NEWSTRING = STRREP(&NEWSTRING.LENGTH, '&NEWSTRING.EVAL', 1, '#', 1, '''', &NEWSTRING.LENGTH, 'A&NEWSTRING.LENGTH');
-SET &NEWSTRING = '''' || &NEWSTRING || '''';
 
-SET &BODYPARM = &NEWSTRING;
 
TABLE FILE CAR
HEADING CENTER
"WHERE BODYTYPE IS &BODYPARM;"
SUM RCOST DCOST
BY COUNTRY
BY BODYTYPE
BY MODEL
WHERE BODYTYPE IS &BODYPARM ;
END
-RUN
 
